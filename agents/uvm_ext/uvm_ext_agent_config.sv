`ifndef UVM_EXT_AGENT_CONFIG_SV
    `define UVM_EXT_AGENT_CONFIG_SV

    class uvm_ext_agent_config#(type VIRTUAL_INTF = int) extends uvm_component;

        `uvm_component_param_utils(uvm_ext_agent_config#(VIRTUAL_INTF))

        // Virtual interface
        protected VIRTUAL_INTF vif;

        // An agent is active when it has a driver, and passive when it doesn't
        // have a driver. This is useful to determine if the agent will drive
        // the signals or just monitor them.
        protected uvm_active_passive_enum active_passive;

        //Switch to enable the checks
        protected bit has_checks;

        //Switch to enable coverage
        protected bit has_coverage;


        function new(string name = "", uvm_component parent);
            super.new(name, parent);

            active_passive = UVM_ACTIVE; // By default, we set the agent to active
            has_checks = 1; // By default, we enable the checks
            has_coverage =1;
        endfunction

        virtual function VIRTUAL_INTF get_vif();
            return vif;
        endfunction

        virtual function void set_vif(VIRTUAL_INTF value);
            // To prevent it is only set once, we check if it is already set before assigning the value
            if (vif == null) begin
                vif = value;
            end
            else begin
                `uvm_fatal("ALGORITHM_ISSUE", "Trying to set the APB virtual interface more than once")
            end
        endfunction


        virtual function uvm_active_passive_enum get_active_passive();
            return active_passive;  
        endfunction

        virtual function void set_active_passive(uvm_active_passive_enum value);
            active_passive = value;
        endfunction

        virtual function bit get_has_checks();
            return has_checks;
        endfunction

        virtual function void set_has_checks(bit value);
            has_checks = value;
        endfunction

        virtual function bit get_has_coverage();
            return has_coverage;
        endfunction

        virtual function void set_has_coverage(bit value);
            has_coverage = value;
        endfunction

        // UVM phase (it can be implemented because it's a uvm_component)
        virtual function void start_of_simulation_phase(uvm_phase phase);
            super.start_of_simulation_phase(phase);
            // We can also check if the virtual interface has been set before the start of the simulation
            if (vif == null) begin
                `uvm_fatal("ALGORITHM_ISSUE", "The virtual interface not configured at the start of the simulation phase")
            end
            else begin
                `uvm_info("UVM_EXT_CONFIG", "The virtual interface configured successfully", UVM_LOW)
            end
        endfunction

        // Task for waiting the reset to start (asynchronous)
        virtual task wait_reset_start();
            `uvm_fatal("ALGORITHM_ISSUE", "The wait_reset_start() task must be implemented in the child class")
        endtask

        // Task for waiting the reset to end (synchronous)
        virtual task wait_reset_end();
            `uvm_fatal("ALGORITHM_ISSUE", "The wait_reset_end() task must be implemented in the child class")
        endtask

    endclass
`endif